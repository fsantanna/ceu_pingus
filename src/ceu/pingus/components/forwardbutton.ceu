#ifndef FORWARDBUTTON_CEU
#define FORWARDBUTTON_CEU

#include "../../c.ceu"
#include "../../sdl.ceu"
#include "../../engine/display/sprite.ceu"
#include "../../engine/gui/igui_component.ceu"
#include "../../proxies/eventproxy.ceu"
#include "../../proxies/primarybuttonproxy.ceu"
#include "../screens/igame_session.ceu"

native/pre do    
    ##define gc_draw(gc, s, rect) gc->draw(*s, Vector2i(rect.left, rect.top))
end

native @pure _gc_draw();

class ForwardButton with
    interface IGUI_Component;
    var IGameSession& session;
do
    var Sprite surface;
    var Sprite background;
    var Sprite backgroundhl;
    call/rec background.reload_by_name("core/buttons/hbuttonbgb");
    call/rec backgroundhl.reload_by_name("core/buttons/hbuttonbg");
    call/rec surface.reload_by_name("core/buttons/fast_forward");

    //freed in GroupComponent
    //finalize with _delete_obj(me_); end

    RECT_IS_AT(&&me);

    par do
        loop do
            await mouse_press;
            session.set_fast_forward(true);

            await mouse_release;
            session.set_fast_forward(false);
        end
    with
        every gc in draw do
            if session.get_fast_forward() then
                _gc_draw(gc, backgroundhl.get(), me.rect);
            else
                _gc_draw(gc, background.get(), me.rect);
            end

            _gc_draw(gc, surface.get(), me.rect);
        end
    end
end

#endif
